plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
}

group 'com.landonwoerdeman'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/${project.findProperty("gpr.repo") ?: System.getenv("REPO_NAME")}")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation(platform('org.junit:junit-bom:5.6.2'))
        testImplementation('org.junit.jupiter:junit-jupiter')
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

project(':ofx-server') {
    dependencies {
        implementation project(':ofx-lib')
    }
}

